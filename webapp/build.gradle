apply plugin: "war"
apply from: "https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin"
apply plugin: "webapp"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

dependencies {
    compile (project(":dbmodel")) {transitive = false}
    compile group: "org.intermine", name: "bio-core", version: imVersion
    compile group: "org.intermine", name: "intermine-api", version: imVersion
    compile group: "org.intermine", name: "intermine-webapp", version: imVersion, classifier: "classes"
    compile group: "org.intermine", name: "intermine-webtasks", version: imVersion
    compile group: "javax.servlet", name: "jstl", version: "1.2"
    compile group: "taglibs", name: "standard", version: "1.1.2"
    compile group: "org.apache.struts", name: "struts-core", version: "1.3.10"
    compile group: 'org.apache.struts', name: 'struts-taglib', version: '1.3.10'
    compile group: 'org.apache.struts', name: 'struts-extras', version: '1.3.10'
    compile group: 'org.apache.struts', name: 'struts-tiles', version: '1.3.10'
    compile group: 'javax.mail', name: 'mail', version: '1.4'
    compile group: 'org.directwebremoting', name: 'dwr', version: '2.0.1'
    runtime group: 'commons-codec', name: 'commons-codec', version: '1.9'
    runtime group: 'taglibs', name: 'string', version: '1.1.0'
    runtime group: 'commons-httpclient', name: 'commons-httpclient', version: '3.0'
    runtime group: 'commons-fileupload', name: 'commons-fileupload', version: '1.2.2'
    runtime group: 'org.biojava', name: 'core', version: '1.9.1'
}

def explodedWebAppDir = "$buildDir/explodedWebApp"
def mergePropsDir = "$buildDir/props"

webappConfig {
    mineName = "flymine"
    objectStoreName = "os.production"
    userProfileObjectStoreName = "os.userprofile-production"
    userProfileObjectStoreWriterName = "osw.userprofile-production"
    defaultInterminePropertiesFile = "default.intermine.production.properties"
    propsDir = "$mergePropsDir"
}

war {
    dependsOn 'mergeProperties', 'summariseObjectStore', 'unwarBioWebApp', 'addStrutsConfig'
    finalizedBy 'warWebApp'
}

task warWebApp(type: War) {
    dependsOn 'copyWebappContent'
    description "Create the war from the build/explodedWebAppDir directory"
    from "${explodedWebAppDir}"
    exclude "WEB-INF/web.properties"
    webInf { from "${mergePropsDir}" }
}

task copyWebappContent(type: Copy) {
    from "src/main/webapp/"
    into "${explodedWebAppDir}"
}

// read in biotestmine.properties to get the contextPath
def props = new Properties()
file(minePropertyFile).withInputStream { props.load(it) }

gretty {
    jvmArgs = ['-Dorg.apache.el.parser.SKIP_IDENTIFIER_CHECK=true']
    contextPath = props.getProperty("webapp.path")
}
